allprojects {
    group = 'simplerogue'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'

    repositories {
        mavenCentral()
    }
}

project(':engine') {
    processResources {
        include '*.xml'
        expand project.properties
    }

    dependencies {
        compile 'com.google.guava:guava:16.0.1'
        compile 'org.apache.commons:commons-lang3:3.3.2'

        compile 'org.projectlombok:lombok:1.12.6'

        compile 'commons-io:commons-io:2.4'
        compile 'com.eclipsesource.minimal-json:minimal-json:0.9.2'

        compile 'org.slf4j:slf4j-api:1.7.6'
        compile 'org.slf4j:slf4j-simple:1.7.6'

        compile 'com.github.xaguzman:pathfinding:0.2.3'

        testCompile 'junit:junit:4.11'
    }
}

project(':engine-test') {
    test {
        workingDir = new File(projectDir, 'build')
    }

    dependencies {
        compile project(':engine')

        compile 'junit:junit:4.11'
        compile 'org.hamcrest:hamcrest-all:1.3'
        compile 'org.mockito:mockito-all:1.9.5'
    }
}

project(':world') {
    dependencies {
        compile project(':engine')
    }
}

project(':platform-javase') {
    apply plugin: 'application'

    mainClassName = 'simplerogue.javase.MainClass'

    dependencies {
        compile project(':engine')
        compile project(':world')
    }

    run {
        if (project.hasProperty('debug')) {
            systemProperties "org.slf4j.simpleLogger.defaultLogLevel": "debug"
        }
    }
}
